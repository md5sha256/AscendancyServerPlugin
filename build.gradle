buildscript {
    repositories {
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
    }
}

plugins {
    id 'org.spongepowered.plugin' version '0.8.1'
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: 'com.github.johnrengelman.shadow'

repositories {
    maven {
        name = 'aikar'
        url = 'https://repo.aikar.co/content/groups/aikar/'
    }
}

dependencies {

    //SpongeAPI
    provided('org.spongepowered:spongeapi:5.1.0') {
        // Exclude sponge 5's configurate, we shade in configurate 4.0.0
        exclude group: 'ninja.leaping.configurate'
    }

    // Jetbrains Annotations
    compileOnly 'org.jetbrains:annotations:19.0.0'

    //AscendancyLib
    compile project(':AscendancyLib')

    // Guice assisted-inject
    compile('com.google.inject.extensions:guice-assistedinject:4.0') {
        exclude group: 'com.google.inject', module: 'guice'
    }

    // ACF
    compile 'co.aikar:acf-sponge:0.5.0-SNAPSHOT'

    // TaskChain
    compile 'co.aikar:taskchain-sponge:3.7.2'

    // Configurate 4.0.0
    implementation('org.spongepowered:configurate-yaml:4.0.0')

}

minecraft {
    forgeVersion = project.properties.forgeVersion
    mappings = project.properties.mcpVersion
}

assemble {
    dependsOn(project.shadowJar)
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileJava {
    // Keep params
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}


shadowJar {
    baseName = archivesBaseName
    classifier = null
    version = modVersion
    relocate 'co.aikar.taskchain', 'com.gmail.andrewandy.ascendancy.serverplugin.lib.taskchain'
    relocate 'co.aikar.commands', 'com.gmail.andrewandy.ascendancy.serverplugin.lib.acf'
    relocate 'co.aikar.locales', 'com.gmail.andrewandy.ascendancy.serverplugin.lib.locales'
    relocate 'io.leagen.geantyref', 'com.gmail.andrewandy.ascendancy.serverplugin.lib.geantyref'
    relocate 'com.google.inject', 'com.gmail.andrewandy.ascendancy.serverplugin.lib.inject'
    relocate 'org.checkerframework', 'com.gmail.andrewandy.ascendancy.serverplugin.lib.checkerframework'
    relocate 'org.yaml.snakeyaml', 'com.gmail.andrewandy.ascendancy.serverplugin.lib.snakeyaml'
    // Relocate configurate to prevent conflicts with sponge (shouldn't be an issue for Sponge 5, may be for Sponge 7.)
    relocate 'org.spongepowered.configurate', 'com.gmail.andrewandy.ascendancy.serverplugin.lib.configurate'
}
